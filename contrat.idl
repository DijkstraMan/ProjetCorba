module modEntreesSortiesZones
{
	typedef string EmpreinteCollab;
	
	typedef string Matricule;
	typedef string NomUsr;
	typedef string PrenomUsr;
	typedef string PasswordPerm;
	typedef string PhotoUsr;
	
	typedef long idZone;
	typedef string JourDebut;
	typedef string JourFin;
	typedef long HeureDebut;
	typedef long HeureFin;
	typedef string dateLog;
	typedef long TimeStamp;
	enum TypeAcces {autorise, nonAutorise, nonAuthentifie}; 
	
	exception EmpreinteInconnue {string raison;};
	exception EmpreinteExistante {string raison;};
	exception UtilisateurInconnu {string raison;};
	exception UtilisateurExistant {string raison;};
	exception AutorisationExistante {string raison;};
	exception AutorisationInconnue {string raison;};
	
	struct Empreinte 
	{
		Matricule matricule;
		EmpreinteCollab empCollab;
	};

	struct Utilisateur
	{
		Matricule matricule;
		NomUsr nomUsr;
		PrenomUsr preUsr;
		PhotoUsr phoUsr;
	};

	struct Autorisation
	{
		Matricule matricule;
		idZone idZone;
		HeureDebut hrDebut;
		HeureFin hrFin;
	};
	
	struct LogAcces
	{
		Matricule matricule;
		idZone idZone;
		TimeStamp t;
		TypeAcces typeAcces;
	};
	
	typedef sequence<Utilisateur> LesUtilisateurs;
        typedef sequence<LogAcces> LogsAcces;
	
	interface ServiceAuthentification
	{
		void modifierUtilisateur(in Matricule matricule, in NomUsr nomUsr, in PrenomUsr preUsr, in PhotoUsr phoUsr) raises (UtilisateurInconnu);
		Utilisateur getUtilisateur(in Matricule matricule) raises (UtilisateurInconnu);
		LesUtilisateurs getUtilisateurs();
		
		//verif pour la porte
		NomUsr verifierAuthentificationPorte(in EmpreinteCollab empCollab, in PhotoUsr phoUsr) raises (UtilisateurInconnu);
		//verif pour la machine
		boolean verifierAuthentificationLogiciel(in Matricule matricule, in PasswordPerm pwd) raises (UtilisateurInconnu);
		//Utilisateurs temporaires :
		void ajouterCollaborateurTemp(in Matricule matricule, in NomUsr nomUsr, in PrenomUsr preUsr, in PhotoUsr phoUsr) raises (UtilisateurExistant); 
		//Utilisateurs permanents :
		void ajouterCollaborateurPerm(in Matricule matricule, in NomUsr nomUsr, in PrenomUsr preUsr, in PhotoUsr phoUsr, in PasswordPerm pwd) raises (UtilisateurExistant);
		//recuperer utilisateurs pour listes
	};

	interface ServiceEmpreinte
	{
		boolean verifierEmpreinte(in EmpreinteCollab empCollab, in Matricule matricule) raises (EmpreinteInconnue);
		void ajouterEmpreinte(in EmpreinteCollab empCollab, in Matricule matricule) raises (EmpreinteExistante);
		void modifierEmpreinte(in EmpreinteCollab empCollab, in Matricule matricule) raises (EmpreinteInconnue);
	};

	interface ServiceAutorisation
	{
		//responsable de zone
		boolean verifierAutorisation(in Matricule matricule, in idZone idZone) raises (AutorisationInconnue); 
		//Autorisations temporaires :
		void ajouterAutorisationTemp(in Matricule matricule, in idZone idZone, in JourDebut jrDebut, in JourFin jrFin) raises (AutorisationExistante);
		void modifierAutorisationTemp(in Matricule matricule, in idZone idZone, in JourDebut jrDebut, in JourFin jrFin) raises (AutorisationInconnue);
		//Autorisations permanentes :
		void ajouterAutorisationPerm(in Matricule matricule, in idZone idZone, in HeureDebut hrDebut, in HeureFin hrFin) raises (AutorisationExistante); 
		void modifierAutorisationPerm(in Matricule matricule, in idZone idZone, in HeureDebut hrDebut, in HeureFin hrFin) raises (AutorisationInconnue); 
		void supprimerAutorisation(in Matricule matricule, in idZone idZone) raises (AutorisationInconnue);
	};
	
	interface ServiceJournalisation
	{
		void ajouterEntree(in Matricule matricule, in idZone idZone, in dateLog t, in TypeAcces typeAcces);
		LogsAcces consulterAcces();
		LogsAcces consulterRefus();
	};
};